plugins {
	id "com.crystaelix.loom" version "jitpack-e3412050"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.5.0"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.parchmentmc.net/"
			}
		}
		filter {
			includeGroup "org.parchmentmc.data"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.covers1624.net/"
			}
		}
		filter {
			includeGroup "codechicken"
			includeGroup "com.brandon3055.brandonscore"
			includeGroup "com.brandon3055.draconicevolution"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "appeng"
			includeGroup "mekanism"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.18.2"
def modName = "PackagedDraconic"
def modId = "packageddraconic"
def specVersion = "3"
def vendor = "thelm"

group = "thelm.packageddraconic"
version = "3.0.0.9"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
		}
		client
		server
	}
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.22.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.18.2"
	mappings loom.layered {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-1.18.2:2022.11.06@zip"
	}
	forge "net.minecraftforge:forge:1.18.2-40.2.17"

	// Dependencies
	modImplementation "curse.maven:packagedauto-308380:5176730"
	modImplementation "codechicken:CodeChickenLib:1.18.2-4.1.4.488:universal"
	modImplementation "com.brandon3055.brandonscore:BrandonsCore:1.18.2-3.1.10.283:universal"
	modImplementation "com.brandon3055.draconicevolution:Draconic-Evolution:1.18.2-3.0.31.531:universal"
	modImplementation("mezz.jei:jei-1.18.2-forge:10.2.1.1006") {transitive = false}
	modImplementation "appeng:appliedenergistics2-forge:11.7.6"
	modImplementation "vazkii.patchouli:Patchouli:1.18.2-71.1"

	// Testing
	modImplementation "mekanism:Mekanism:1.18.2-10.2.5.465"
	modImplementation "curse.maven:applied-mekanistics-574300:3968835"

	// Performance
	modImplementation "curse.maven:yeetusexperimentus-635427:4377180"
	implementation "io.github.llamalad7:mixinextras-forge:0.3.5"
	modImplementation "curse.maven:modernfix-790626:5157929"
	modImplementation "curse.maven:ferritecore-429235:4074294"
	modImplementation "curse.maven:lazy-dfu-forge-460819:3544496"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-${project.version}"
	changelog = file("changelog.md").text
	type = BETA
	modLoaders.add("forge")
	additionalFiles.from(remapSourcesJar.archiveFile)

	curseforge {
		accessToken = providers.gradleProperty("curseforge.token")
		projectId = "833931"
		minecraftVersions.add(mcVersion)
		requires "packagedauto", "draconic-evolution"
		optional "applied-energistics-2"
	}

	modrinth {
		accessToken = providers.gradleProperty("modrinth.token")
		projectId = "dNduUQBR"
		minecraftVersions.add(mcVersion)
		requires "packagedauto"
		optional "ae2"
	}
}
